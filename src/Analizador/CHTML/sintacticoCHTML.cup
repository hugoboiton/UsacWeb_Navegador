package Analizador.CHTML;

import java_cup.runtime.Symbol;
import EstructurasDatos.NodoArbol;
import EstructurasDatos.NodoError;
import java.util.ArrayList;
//parser chtml

parser code
{:
    public String archivo="null";
    public static ArrayList<NodoError> sitactico= new ArrayList<NodoError>();
    public void agregarerror(int linea,int columna,String tipo,String men)
    {
       NodoError er= new NodoError(linea, columna, tipo, men,archivo);
       sitactico.add(er);
    }
    public static String errorSin="";
    public void syntax_error(Symbol s){
         agregarerror(s.left+1,s.right,"sintacitico",s.value.toString());
         
         
    }
    public  ArrayList<NodoError> Sin()
    {
        return sitactico;
    }
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
         //agregarerror(s.left+1,s.right,"sintacitico",s.value.toString());   
         
    }

:}
action code
{:
    // acciones que podemos hacer en le codigo
    NodoArbol raiz;
    NodoArbol padre,izq,der,nodo;
    private NodoArbol CrearHoja(String produccion,String valor)
    {
        NodoArbol nodo= new NodoArbol(produccion,valor,0,0);
        return nodo;
    }
    private NodoArbol AddHijo(NodoArbol raiz,NodoArbol hijo){
            raiz.add(hijo);
            return raiz;
     }
    private NodoArbol cambiarHijo(NodoArbol padre,NodoArbol hijos)
    {
        padre.hijos=hijos.hijos;
        return padre;

    }
    private String QuitarComillas(String cadena)
    {
        cadena=cadena.replace('\"', ' ');
        return cadena.trim();
    }

:}

terminal String CHTML,FINCHTML,ENCABEZADO,FINENCABEZADO,CJS,FINCJS,CCSS,FINCCSS,         
TITULO,FINTITULO,CUERPO,FINCUERPO,PANEL,FINPANEL,IMAGEN,FINIMAGEN,BOTON,FINBOTON,        
ENLACE,FINENLACE;

terminal String TEXTO,FINTEXTO,CAJATEXTO,FINCAJATEXTO,TEXTOA,FINTEXTOA;

terminal String TABLA,FINTABLA,FILT,FINFILT,CB,FINCB,CT,FINCT;

terminal String CAJA,FINCAJA,OPCION,FINOPCION,VALOR,SPINNER, 
FINSPINNER,SALTOFIN,CLICK,RUTA,FONDO;

terminal String ID,GRUPO,ALTO,ANCHO,ALINEADO,ABRIR,CERRAR,           
PUNTOCOMA,IGUAL,ECADENA,ETEXTO;

nonterminal NodoArbol inicio;
nonterminal NodoArbol encabezado,lcompencabezado,compencabezado;
nonterminal NodoArbol titulo,ccss,cjs;
nonterminal NodoArbol cuerpo,lcompcuerpo,compcuerpo;       

nonterminal NodoArbol ruta,click;

nonterminal NodoArbol lopciones , opciones, id, grupo, ancho , alto ,alineado,fondo;

nonterminal NodoArbol panel,texto,imagen,boton,enlace;

nonterminal NodoArbol tabla,celdas,celda,lct,ct,cuerpotabla,lcuerpotabla;

nonterminal NodoArbol textoa,cajatexto;

nonterminal NodoArbol caja,lopcion,opcion,valor;

nonterminal NodoArbol spinner,saltofin;


start with inicio;

inicio  ::= ABRIR CHTML CERRAR encabezado:en cuerpo:cu ABRIR FINCHTML CERRAR 
        {:
            System.out.println("Gramatica corecta");
            padre=CrearHoja(ConstantesCHTML.inicio,"");
            padre=AddHijo(padre,en);
            padre=AddHijo(padre,cu);
            raiz=padre;
            RESULT=raiz;

        :}
        |ABRIR CHTML CERRAR  ABRIR FINCHTML CERRAR
        {:
            System.out.println("Gramatica corecta");
            padre=CrearHoja(ConstantesCHTML.inicio,"");
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.encabezado,""));
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.cuerpo,""));
            raiz=padre;
            RESULT=raiz;
        :};

encabezado ::= ABRIR ENCABEZADO CERRAR lcompencabezado:en ABRIR FINENCABEZADO CERRAR
            {:
                RESULT=en;
            :}
            |ABRIR ENCABEZADO CERRAR ABRIR FINENCABEZADO CERRAR
            {:
               RESULT=CrearHoja(ConstantesCHTML.encabezado,"");
            :};

lcompencabezado ::= lcompencabezado:lc compencabezado:nodo
                {:
                    lc=AddHijo(lc,nodo);
                    RESULT=lc;
                :}
                | compencabezado:nodo
                {:
                    padre=CrearHoja(ConstantesCHTML.encabezado,"");
                    padre=AddHijo(padre,nodo);
                    RESULT=padre;
                :};
                

compencabezado  ::= titulo:nodo{:RESULT=nodo;:}
                |ccss:nodo{:RESULT=nodo;:}
                |cjs:nodo{:RESULT=nodo;:}
                |error ABRIR{:RESULT=CrearHoja(ConstantesCHTML.error,"");:};

titulo     ::= ABRIR TITULO CERRAR ETEXTO:text ABRIR FINTITULO CERRAR
           {:
               RESULT=CrearHoja(ConstantesCHTML.titulo,text);
            :}
           | ABRIR TITULO CERRAR ABRIR FINTITULO CERRAR
           {:
                 RESULT=CrearHoja(ConstantesCHTML.titulo,"");
            :};

ccss       ::= ABRIR CCSS lopciones:op CERRAR ABRIR FINCCSS CERRAR
           {:
                   padre=CrearHoja(ConstantesCHTML.ccss,"");
                   padre=AddHijo(padre,op);
                   RESULT=padre;
            :}
           | ABRIR CCSS CERRAR ECADENA:cad ABRIR FINCCSS CERRAR
           {:
                    RESULT=CrearHoja(ConstantesCHTML.ccss,QuitarComillas(cad));
            :};

cjs        ::= ABRIR CJS lopciones:op  CERRAR  ABRIR FINCJS CERRAR
           {:
                   padre=CrearHoja(ConstantesCHTML.cjs,"");
                   padre=AddHijo(padre,op);
                   RESULT=padre;
            :}
           | ABRIR CJS CERRAR ECADENA:cad ABRIR FINCJS CERRAR
           {:
                RESULT=CrearHoja(ConstantesCHTML.cjs,QuitarComillas(cad));
            :};


cuerpo     ::= ABRIR CUERPO lopciones:op CERRAR lcompcuerpo:cuerpo ABRIR FINCUERPO CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.cuerpo,"");
                padre=AddHijo(padre,op);
                padre=AddHijo(padre,cuerpo);
                RESULT=padre;
            :}
           |ABRIR CUERPO lopciones:op CERRAR  ABRIR FINCUERPO CERRAR
           {:
                padre=CrearHoja(ConstantesCHTML.cuerpo,"");
                padre=AddHijo(padre,op);
                izq=CrearHoja(ConstantesCHTML.compcuerpo,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
           |ABRIR CUERPO CERRAR lcompcuerpo:cuerpo ABRIR FINCUERPO CERRAR
           {:
                padre=CrearHoja(ConstantesCHTML.cuerpo,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                padre=AddHijo(padre,cuerpo);
                RESULT=padre;

            :} 
           |ABRIR CUERPO CERRAR  ABRIR FINCUERPO CERRAR
           {:
                padre=CrearHoja(ConstantesCHTML.cuerpo,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                izq=CrearHoja(ConstantesCHTML.compcuerpo,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;

            :};

lcompcuerpo ::= lcompcuerpo:co compcuerpo:nodo
            {:
                co=AddHijo(co,nodo);
                RESULT=co;
            :}
            | compcuerpo:nodo
            {:
                padre=CrearHoja(ConstantesCHTML.compcuerpo,"");
                padre=AddHijo(padre,nodo);
                RESULT=padre;
            :};  

compcuerpo  ::= panel:nodo{:RESULT=nodo;:}
            |texto:nodo{:RESULT=nodo;:}
            |imagen:nodo{:RESULT=nodo;:}
            |boton:nodo{:RESULT=nodo;:}
            |enlace:nodo{:RESULT=nodo;:}
            |tabla:nodo{:RESULT=nodo;:}
            |textoa:nodo{:RESULT=nodo;:}
            |cajatexto:nodo{:RESULT=nodo;:}
            |caja:nodo{:RESULT=nodo;:}
            |spinner:nodo{:RESULT=nodo;:}
            |saltofin:nodo{:RESULT=nodo;:}
            |error ABRIR{:RESULT=CrearHoja(ConstantesCHTML.error,"");:};


panel       ::= ABRIR PANEL lopciones:op CERRAR lcompcuerpo:cuerpo ABRIR FINPANEL CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.panel,"");
                padre=AddHijo(padre,op);
                padre=AddHijo(padre,cuerpo);
                RESULT=padre;

            :}
            | ABRIR PANEL CERRAR lcompcuerpo:cuerpo ABRIR FINPANEL CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.panel,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                padre=AddHijo(padre,cuerpo);
                RESULT=padre;
            :}
            | ABRIR PANEL lopciones:op CERRAR  ABRIR FINPANEL CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.panel,"");
                padre=AddHijo(padre,op);
                izq=CrearHoja(ConstantesCHTML.cuerpo,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            | ABRIR PANEL CERRAR ABRIR FINPANEL CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.panel,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                izq=CrearHoja(ConstantesCHTML.cuerpo,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :};

texto       ::= ABRIR TEXTO lopciones:op CERRAR ETEXTO:text ABRIR FINTEXTO CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.texto,text);
                padre=AddHijo(padre,op);
                RESULT=padre;

            :}
            |ABRIR TEXTO CERRAR ETEXTO:text ABRIR FINTEXTO CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.texto,text);
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            |ABRIR TEXTO lopciones:op CERRAR  ABRIR FINTEXTO CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.texto,"");
                padre=AddHijo(padre,op);
                RESULT=padre;
            :}
            |ABRIR TEXTO CERRAR  ABRIR FINTEXTO CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.texto,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;

            :};

imagen      ::= ABRIR IMAGEN CERRAR ETEXTO:text ABRIR FINIMAGEN CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.imagen,text);
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            |ABRIR IMAGEN lopciones:op CERRAR ETEXTO:text ABRIR FINIMAGEN CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.imagen,text);
                padre=AddHijo(padre,op);
                RESULT=padre;
            :}
            |ABRIR IMAGEN CERRAR  ABRIR FINIMAGEN CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.imagen,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            |ABRIR IMAGEN lopciones:op CERRAR  ABRIR FINIMAGEN CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.imagen,"");
                padre=AddHijo(padre,op);
                RESULT=padre;
            :};   

boton       ::= ABRIR BOTON lopciones:op CERRAR ETEXTO:text ABRIR FINBOTON CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.boton,text);
                padre=AddHijo(padre,op);
                RESULT=padre;

            :}
            | ABRIR BOTON CERRAR ETEXTO:text ABRIR FINBOTON CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.boton,text);
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            | ABRIR BOTON CERRAR ABRIR FINBOTON CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.boton,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            | ABRIR BOTON lopciones:op CERRAR ABRIR FINBOTON CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.boton,"");
                padre=AddHijo(padre,op);
                RESULT=padre;
            :};

enlace      ::= ABRIR ENLACE lopciones:op CERRAR ETEXTO:text ABRIR FINENLACE CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.enlace,text);
                padre=AddHijo(padre,op);
                RESULT=padre;
            :}
            |ABRIR ENLACE CERRAR ETEXTO:text ABRIR FINENLACE CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.enlace,text);
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            |ABRIR ENLACE CERRAR  ABRIR FINENLACE CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.enlace,"");
                izq=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,izq);
                RESULT=padre;
            :}
            |ABRIR ENLACE lopciones:op CERRAR ABRIR FINENLACE CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.enlace,"");
                padre=AddHijo(padre,op);
                RESULT=padre;
            :};

tabla       ::= ABRIR TABLA lopciones:lop CERRAR celdas:c ABRIR FINTABLA CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.tabla,"");
                padre=AddHijo(padre,lop);
                padre=AddHijo(padre,c);
                RESULT=padre;
            :}
            |ABRIR TABLA CERRAR celdas:c ABRIR FINTABLA CERRAR
            {:
                padre=CrearHoja(ConstantesCHTML.tabla,"");
                padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
                padre=AddHijo(padre,c);
                RESULT=padre;
            :};

celdas  ::= celdas:ce celda:c
        {:
            ce=AddHijo(ce,c);
            RESULT=ce;
        :}
        | celda:c
        {:
            padre=CrearHoja(ConstantesCHTML.celdas,"");
            padre=AddHijo(padre,c);
            RESULT=padre;
        :};   

celda ::= ABRIR FILT CERRAR lct:c ABRIR FINFILT CERRAR{:RESULT=c;:};

lct ::= lct:lc ct:c
    {:
        lc=AddHijo(lc,c);
        RESULT=lc;
    :}
    |ct:c
    {:
        padre=CrearHoja(ConstantesCHTML.celda,"");
        padre=AddHijo(padre,c);
        RESULT=padre;

    :};

ct ::= ABRIR CT CERRAR lcuerpotabla:lc ABRIR FINCT CERRAR
    {:
        padre=CrearHoja(ConstantesCHTML.ct,"");
        padre=AddHijo(padre,lc);
        RESULT=padre;
    :}
   | ABRIR CT CERRAR  ABRIR FINCT CERRAR
   {:
        padre=CrearHoja(ConstantesCHTML.ct,"");
        padre=AddHijo(padre,CrearHoja(ConstantesCHTML.cuerpotabla,""));
        RESULT=padre;
    :}
   |ABRIR CB CERRAR lcuerpotabla:lc ABRIR FINCB CERRAR
   {:
        padre=CrearHoja(ConstantesCHTML.cb,"");
        padre=AddHijo(padre,lc);
        RESULT=padre;
    :}
   | ABRIR CB CERRAR ABRIR FINCB CERRAR
   {:
        padre=CrearHoja(ConstantesCHTML.cb,"");
        padre=AddHijo(padre,CrearHoja(ConstantesCHTML.cuerpotabla,""));
        RESULT=padre;
   :};


lcuerpotabla ::= lcuerpotabla:lc cuerpotabla:nodo
            {:
                lc=AddHijo(lc,nodo);
                RESULT=lc;
            :}
            |cuerpotabla:nodo
            {:
                padre=CrearHoja(ConstantesCHTML.cuerpotabla,"");
                padre=AddHijo(padre,nodo);
                RESULT=padre;
            :};

cuerpotabla ::= texto:nodo{:RESULT=nodo;:}
            |textoa:nodo{:RESULT=nodo;:}
            |cajatexto:nodo{:RESULT=nodo;:}
            |imagen:nodo{:RESULT=nodo;:}
            |boton:nodo{:RESULT=nodo;:}
            |ETEXTO
            |error ABRIR{:RESULT=CrearHoja(ConstantesCHTML.error,"");:};

textoa ::= ABRIR TEXTOA lopciones:lop CERRAR ETEXTO:text ABRIR FINTEXTOA CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.textoa,text);
            padre=AddHijo(padre,lop);
            RESULT=padre;
        :}
       |ABRIR TEXTOA CERRAR ETEXTO:text ABRIR FINTEXTOA CERRAR
       {:
            padre=CrearHoja(ConstantesCHTML.textoa,text);
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :}
       |ABRIR TEXTOA lopciones:lop CERRAR  ABRIR FINTEXTOA CERRAR
       {:
            padre=CrearHoja(ConstantesCHTML.textoa,"");
            padre=AddHijo(padre,lop);
            RESULT=padre;
       :}
       |ABRIR TEXTOA CERRAR  ABRIR FINTEXTOA CERRAR
       {:
            padre=CrearHoja(ConstantesCHTML.textoa,"");
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :};

cajatexto ::= ABRIR CAJATEXTO lopciones:lop CERRAR ETEXTO:text ABRIR FINCAJATEXTO CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.cajatexto,text);
            padre=AddHijo(padre,lop);
            RESULT=padre;
        :}
       |ABRIR CAJATEXTO CERRAR ETEXTO:text ABRIR FINCAJATEXTO CERRAR
       {:
            padre=CrearHoja(ConstantesCHTML.cajatexto,text);
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :}
       |ABRIR CAJATEXTO lopciones:lop CERRAR  ABRIR FINCAJATEXTO CERRAR
       {:
            padre=CrearHoja(ConstantesCHTML.cajatexto,"");
            padre=AddHijo(padre,lop);
            RESULT=padre;
      :}
       |ABRIR CAJATEXTO CERRAR  ABRIR FINCAJATEXTO CERRAR
       {:
            padre=CrearHoja(ConstantesCHTML.cajatexto,"");
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
       :};

caja ::= ABRIR CAJA lopciones:op CERRAR lopcion:lop ABRIR FINCAJA CERRAR
     {:
        padre=CrearHoja(ConstantesCHTML.caja,"");
        padre=AddHijo(padre,op);
        padre=AddHijo(padre,lop);
        RESULT=padre;

     :}
     | ABRIR CAJA CERRAR lopcion:lop ABRIR FINCAJA CERRAR
     {:
        padre=CrearHoja(ConstantesCHTML.caja,"");
        padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
        padre=AddHijo(padre,lop);
        RESULT=padre;
     :}
     | ABRIR CAJA lopciones:op  CERRAR  ABRIR FINCAJA CERRAR
     {:
        padre=CrearHoja(ConstantesCHTML.caja,"");
        padre=AddHijo(padre,op);
        padre=AddHijo(padre,CrearHoja(ConstantesCHTML.lopcion,""));
        RESULT=padre;
    :}
     | ABRIR CAJA  CERRAR  ABRIR FINCAJA CERRAR
     {:
        padre=CrearHoja(ConstantesCHTML.caja,"");
        padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
        padre=AddHijo(padre,CrearHoja(ConstantesCHTML.lopcion,""));
        RESULT=padre;
     :};

lopcion ::= lopcion:lop opcion:op
        {:
            lop=AddHijo(lop,op);
            RESULT=lop;
        :}
        |opcion:op
        {:
            padre=CrearHoja(ConstantesCHTML.lopcion,"");
            padre=AddHijo(padre,op);
            RESULT=op;
        :};

opcion  ::= ABRIR OPCION lopciones:lop CERRAR ETEXTO:text ABRIR FINOPCION CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.opcion,text);
            padre=AddHijo(padre,lop);
            RESULT=padre;
        :}
        | ABRIR OPCION lopciones:lop CERRAR  ABRIR FINOPCION CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.opcion,"");
            padre=AddHijo(padre,lop);
            RESULT=padre;
        :}
        | ABRIR OPCION CERRAR ETEXTO:text ABRIR FINOPCION CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.opcion,text);
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :}
        | ABRIR OPCION CERRAR  ABRIR FINOPCION CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.opcion,"");
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :};


spinner ::= ABRIR SPINNER lopciones:lop CERRAR ETEXTO:text ABRIR FINSPINNER CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.spinner,text);
            padre=AddHijo(padre,lop);
            RESULT=padre;
        :} 
        | ABRIR SPINNER CERRAR ETEXTO:text ABRIR FINSPINNER CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.spinner,text);
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :}
        | ABRIR SPINNER lopciones:lop CERRAR  ABRIR FINSPINNER CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.spinner,"0");
            padre=AddHijo(padre,lop);
            RESULT=padre;
        :}
        | ABRIR SPINNER  CERRAR  ABRIR FINSPINNER CERRAR
        {:
            padre=CrearHoja(ConstantesCHTML.spinner,"0");
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :};

saltofin ::= ABRIR SALTOFIN lopciones:lop CERRAR
         {:
                padre=CrearHoja(ConstantesCHTML.saltofin,"");
                padre=AddHijo(padre,lop);
                RESULT=padre;
        :} 
         |ABRIR SALTOFIN CERRAR
         {:
            padre=CrearHoja(ConstantesCHTML.saltofin,"");
            padre=AddHijo(padre,CrearHoja(ConstantesCHTML.opciones,""));
            RESULT=padre;
        :};

ruta        ::= RUTA IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.ruta,QuitarComillas(cade));:};

click       ::= CLICK IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.click,QuitarComillas(cade));:};

lopciones   ::= lopciones:lop opciones:op
            {:
                lop =AddHijo(lop,op);
                RESULT = lop;
            :}
            |opciones:op
            {:
                padre=CrearHoja(ConstantesCHTML.opciones,"");
                padre=AddHijo(padre,op);
                RESULT = padre;
            :};

opciones    ::= id:nodo{:RESULT=nodo;:}
            |grupo:nodo{:RESULT=nodo;:}
            |ancho:nodo{:RESULT=nodo;:}
            |alto:nodo{:RESULT=nodo;:}
            |alineado:nodo{:RESULT=nodo;:}
            |click:nodo{:RESULT=nodo;:}
            |ruta:nodo{:RESULT=nodo;:}
            |fondo:nodo{:RESULT=nodo;:}
            |valor:nodo{:RESULT=nodo;:}
            |error PUNTOCOMA{:RESULT=CrearHoja(ConstantesCHTML.error,"");:};
            

id          ::= ID IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.id,QuitarComillas(cade));:};
grupo       ::= GRUPO IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.grupo,QuitarComillas(cade));:};
alto        ::= ALTO IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.alto,QuitarComillas(cade));:};
ancho       ::= ANCHO IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.ancho,QuitarComillas(cade));:};
alineado    ::= ALINEADO IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.alineado,QuitarComillas(cade));:};
fondo       ::= FONDO IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja( ConstantesCHTML.fondo,QuitarComillas(cade));:};
valor       ::= VALOR IGUAL ECADENA:cade PUNTOCOMA{:RESULT=CrearHoja(ConstantesCHTML.valor,QuitarComillas(cade));:};
